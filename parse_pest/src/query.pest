query = { SOI ~ path ~ EOI }
path = { ((absolute ~ segment ~ filter?) | relative ~ filter?) ~ ("/" ~ segment ~ filter?)* }
absolute = @{ "/" }
relative = @{ "." }
segment = { wildcard | ident | integer }
wildcard = @{ "*" }
filter = { "[" ~ filter_cond ~ "]" } 
filter_cond = { path ~ op_compare ~ op_rhs }
op_rhs = { path | literal }
op_compare = @{ "?"? ~ ("<=" | ">=" | "=" | ">" | "<") }
ident = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
literal = {integer | string}
integer = @{ "0" | (!("0") ~ ASCII_DIGIT+) }
// TODO handle escape sequences in strings
string = @{ "\"" ~ (!("\"") ~ ANY)* ~ "\""} 
WHITESPACE = _{" " | "\t" | "\r" | "\n"}
