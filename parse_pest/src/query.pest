query = { SOI ~ path ~ EOI }
path = {
    (absolute | selector | relative) ~ selector*
}
selector = { segment | filter | call }
absolute = @{ &"/" }
relative = @{ "." ~ !"." }
segment = { "/" ~ (wildcard | "key:" ~ literal | ident | integer) }
wildcard = @{ "*" }
filter = { "[" ~ path ~ op_compare ~ op_rhs ~ "]" }
op_rhs = { path | literal | var }
var = { "$" ~ ident }
call = { "." ~ ident ~ "("  ~ ")" }
op_compare = @{ "?"? ~ ("<=" | ">=" | "!=" | "=" | ">" | "<") }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
literal = { array | bool | integer | string }
array = { "{" ~ (literal ~ ("," ~ literal)* ~ ","?)? ~ "}" }
bool = @{ "true" | "false" }
integer = @{ "0" | !"0" ~ ASCII_DIGIT+ }
// TODO handle escape sequences in strings
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\""} 
WHITESPACE = _{" " | "\t" | "\r" | "\n"}
